/*
 * Copyright (c) 2018. Lorem ipsum dolor sit amet, consectetur adipiscing elit.
 * Morbi non lorem porttitor neque feugiat blandit. Ut vitae ipsum eget quam lacinia accumsan.
 * Etiam sed turpis ac ipsum condimentum fringilla. Maecenas magna.
 * Proin dapibus sapien vel ante. Aliquam erat volutpat. Pellentesque sagittis ligula eget metus.
 * Vestibulum commodo. Ut rhoncus gravida arcu.
 */

ext {

    debugDetail = [
            'name'       : "Year in Progress Debug",
            'packageName': "com.kevalpatel2106.yip",
    ]

    releaseDetail = [
            'name'       : "Year in Progress",
            'packageName': "com.kevalpatel2106.yip",
    ]

    sdk = [
            'compileSdk': 28,
            'targetSdk' : 28,
            'minSdk'    : 21,
    ]

    playStoreVersion = [
            "version_code": 3,
            "major"       : 0,
            "minor"       : 3,
            "patch"       : 0
    ]

    versions = [
            'kotlin'       : '1.3.11',
            'daggerVersion': "2.21",
            'stetho'       : '1.5.0',
            'room'         : '2.1.0-alpha03',
            'espresso'     : "3.1.1",
            'mockito'      : "2.19.0"
    ]

    packageExcludes = {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
        exclude 'protobuf.meta'
    }

    //Exclude group for dependency
    excludeGrp = {
        // Android X
        exclude group: 'androidx.legacy', module: 'legacy-support-v13'
        exclude group: 'androidx.legacy', module: 'legacy-support-v4'
        exclude group: 'androidx.annotation', module: 'annotation'
        exclude group: 'androidx.appcompat', module: 'appcompat'
        exclude group: 'com.google.android.material', module: 'material'
        exclude group: 'androidx.constraintlayout', module: 'constraintlayout'

        // Android test won't work if we don't remove
        exclude group: 'org.xmlpull'
        exclude group: 'xpp3'

        // Rx
        exclude group: 'io.reactivex.rxjava2', module: 'rxjava'
        exclude group: 'io.reactivex.rxjava2', module: 'rxandroid'

        exclude group: 'com.jakewharton.timber', module: 'timber'
    }

    signingConfig = {
        //This are the play store signing config.
        playStore {
            if (System.getenv("CI") == "true") {

                storeFile file(System.getenv('KEY_PATH'))
                storePassword System.getenv('KEY_PASSWORD')
                keyAlias System.getenv('KEY_ALIAS')
                keyPassword System.getenv('ALIAS_PASSWORD')
            } else {
                File credentials = rootProject.file('./misc/keystore.properties')
                if (credentials.exists()) {
                    Properties properties = new Properties()
                    properties.load(credentials.newDataInputStream())

                    storeFile file(properties.getProperty('KEY_PATH'))
                    storePassword properties.getProperty('KEY_PASSWORD')
                    keyAlias properties.getProperty('KEY_ALIAS')
                    keyPassword properties.getProperty('ALIAS_PASSWORD')
                } else {
                    logger.error('Release keystore.properties not found. Cannot generate release build.')
                }
            }
        }
        //This are the debug signing config
        debug {
            storeFile file('./misc/debug.jks')
            storePassword 'photoloc'
            keyAlias 'kevalpatel2106'
            keyPassword 'photoloc'
        }
    }

    defaultAppConfig = {
        multiDexEnabled false

        applicationId releaseDetail.packageName

        minSdkVersion sdk.minSdk
        targetSdkVersion sdk.targetSdk

        //Versions config
        versionCode playStoreVersion.version_code
        versionName "$playStoreVersion.major.$playStoreVersion.minor.$playStoreVersion.patch"

        //strip other than english resources
        resConfigs "en"

        testInstrumentationRunner "com.kevalpatel2106.battery.monitor.testutils.CustomTestRunner"
    }

    libDefaultConfig = {
        minSdkVersion sdk.minSdk
        targetSdkVersion sdk.targetSdk

        //strip other than english resources
        resConfigs "en"

        //Versions config
        versionCode playStoreVersion.version_code
        versionName "$playStoreVersion.major.$playStoreVersion.minor.$playStoreVersion.patch"

        consumerProguardFiles '../proguard-rules.pro'

        testInstrumentationRunner "com.kevalpatel2106.battery.monitor.testutils.CustomTestRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/repo/schemas".toString()]
            }
        }
    }

    lintOption = {
        abortOnError true
        htmlReport true
        xmlReport true
        textReport true
        checkTestSources true
        ignoreWarnings true

        disable 'InvalidPackage', 'GradleCompatible'
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        check 'NewApi', 'InlinedApi'
    }

    compileOption = {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOption = {
        execution 'ANDROID_TEST_ORCHESTRATOR'
        animationsDisabled = true
        unitTests {
            returnDefaultValues true
            all {
                includeAndroidResources = true

                maxParallelForks 4

                minHeapSize = "1024m"
                maxHeapSize = "1596m"

                testLogging {
                    events 'passed', 'skipped', 'failed'
                }
            }
        }
        reportDir "$rootDir/reports/test-reports"
        resultsDir "$rootDir/reports/test-results"
    }

    kotlinOption = {
        freeCompilerArgs = ['-Xno-param-assertions']
        allWarningsAsErrors = true
        jvmTarget = "1.8"
    }

    aaptOption = {
        cruncherProcesses 2
    }

    adbOption = {
        timeOutInMs 10_000
    }

    dexOption = {
        javaMaxHeapSize = "1024"
        maxProcessCount = 4
        threadCount = 4
        preDexLibraries = true
    }
}
