buildscript {
    apply from: './gradle/config.gradle'
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "com.vanniktech:gradle-android-junit-jacoco-plugin:0.13.0"
        classpath 'com.google.gms:google-services:4.3.2'
        classpath 'io.fabric.tools:gradle:1.28.1'
        classpath 'com.google.android.gms:oss-licenses-plugin:0.9.5'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
        classpath('com.google.firebase:perf-plugin:1.3.1') {
            exclude group: 'com.google.guava', module: 'guava-jdk5'
        }
    }
}
apply plugin: 'com.vanniktech.android.junit.jacoco'
apply from: './gradle/config.gradle'

allprojects {
    repositories {
        google()
        jcenter()
    }

    afterEvaluate {
        extensions.findByName('kapt')?.arguments {
            // https://speakerdeck.com/jakewharton/helping-dagger-help-you-droidcon-uk-2018
            arg('dagger.formatGeneratedSource', 'disabled')
            arg('dagger.gradle.incremental', 'enabled')
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions kotlinOption
    }
}
def packageNameKey = "app_package_name"
def applicationNameKey = "application_name"

subprojects {
    afterEvaluate { project ->
        apply from: "${project.rootDir}/gradle/config.gradle"

        if (project.hasProperty("android")) {   //All the android projects

            //Apply all the configs for the android block here.
            android {
                compileSdkVersion sdk.compileSdk
                signingConfigs signingConfig

                if (project.pluginManager.hasPlugin("com.android.application")) {
                    //This is the app module
                    defaultConfig defaultAppConfig

                    buildTypes {
                        debug {
                            versionNameSuffix ".debug"

                            testCoverageEnabled true
                            debuggable true
                            zipAlignEnabled true
                            signingConfig signingConfigs.debug

                            // Fabric config
                            ext.enableCrashlytics = false
                            ext.alwaysUpdateBuildId = false
                        }
                        release {
                            minifyEnabled true
                            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), './proguard-rules.pro'

                            debuggable false
                            jniDebuggable false
                            shrinkResources true
                            renderscriptDebuggable false
                            zipAlignEnabled true

                            signingConfig signingConfigs.playStore
                        }
                    }
                } else if (project.pluginManager.hasPlugin("com.android.library")) {
                    //This is the library module
                    defaultConfig libDefaultConfig

                    buildTypes {
                        debug {
                            testCoverageEnabled true
                            debuggable true
                            signingConfig signingConfigs.debug
                            zipAlignEnabled true

                            buildConfigField "String", applicationNameKey.toUpperCase(), "\"${debugDetail.name}\""
                            buildConfigField "String", packageNameKey.toUpperCase(), "\"${debugDetail.packageName}\""
                        }
                        release {
                            minifyEnabled true
                            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), './proguard-rules.pro'

                            debuggable false
                            jniDebuggable false
                            renderscriptDebuggable false
                            zipAlignEnabled true
                            signingConfig signingConfigs.playStore

                            buildConfigField "String", applicationNameKey.toUpperCase(), "\"${releaseDetail.name}\""
                            buildConfigField "String", packageNameKey.toUpperCase(), "\"${releaseDetail.packageName}\""
                        }
                    }
                }

                //General options
                compileOptions compileOption
                dexOptions dexOption
                packagingOptions packageExcludes
                lintOptions lintOption
                aaptOptions aaptOption
                testOptions testOption
                androidExtensions { experimental = true }
                dataBinding { enabled = true }
                dexcount dexcountOptions
            }
        }

        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"
            implementation('com.jakewharton.timber:timber:4.7.1', excludeGrp)
            implementation "io.reactivex.rxjava2:rxjava:2.2.5"

            //Dagger dependencies
            implementation "com.google.dagger:dagger-android:${versions.daggerVersion}"
            kapt "com.google.dagger:dagger-compiler:${versions.daggerVersion}"
            kaptAndroidTest "com.google.dagger:dagger-compiler:${versions.daggerVersion}"
            compileOnly 'javax.annotation:jsr250-api:1.0'

            kapt "com.google.dagger:dagger-android-processor:${versions.daggerVersion}"
            kaptAndroidTest "com.google.dagger:dagger-android-processor:${versions.daggerVersion}"

            //Android libs
            if (project.hasProperty("android")) {
                implementation "io.reactivex.rxjava2:rxandroid:2.1.0"

                //Support libs
                implementation "androidx.annotation:annotation:1.1.0"
                implementation "androidx.appcompat:appcompat:1.0.2"
                implementation 'com.google.android.material:material:1.1.0-alpha09'
                implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

                // ktx
                implementation('androidx.core:core-ktx:1.1.0-alpha03', excludeGrp)
                implementation('androidx.fragment:fragment-ktx:1.1.0-alpha03', excludeGrp)
                implementation('androidx.collection:collection-ktx:1.1.0-alpha01', excludeGrp)

                //Arch
                implementation("androidx.lifecycle:lifecycle-common-java8:2.1.0-alpha01", excludeGrp)
                implementation("androidx.lifecycle:lifecycle-extensions:2.1.0-alpha01", excludeGrp)
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
